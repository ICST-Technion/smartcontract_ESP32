#
#  PRE: if
#

# Use pre-defined date and time
&Tmp-Integer-0 := 1506101100

# Convert to string representation
&Tmp-String-0 := %(date:%{Tmp-Integer-0})

# Some systems report GMT some UTC...
if (&Tmp-String-0 != "Fri 22 Sep 17:25:00 GMT 2017") && (&Tmp-String-0 != "Fri 22 Sep 17:25:00 UTC 2017") {
	test_fail
}

# Convert string to integer
&Tmp-Integer-1 := %(integer:%(date:%{Tmp-String-0}))

if (&Tmp-Integer-1 != &Tmp-Integer-0) {
	test_fail
}

# Compare two methods of reading request timestamp in local timezone
&Tmp-String-0 := %(localdate:request)
&Tmp-String-1 := "%S"

if (&Tmp-String-0 != &Tmp-String-1) {
	test_fail
}

# Convert different string format
&Tmp-String-2 := "2017-09-22 17:25:00"

&Tmp-Integer-2 := %(integer:%(sqldate:%{Tmp-String-2}))

if (&Tmp-Integer-2 != &Tmp-Integer-0) {
	test_fail
}

# Use a date attribute
&Tmp-Date-0 := 1659985459
&Tmp-String-2 := "%(sqldate:%{Tmp-Date-0})"

if (&Tmp-String-2 != '2022-08-08 19:04:19') {
	test_fail
}

# Invalid format
&Tmp-String-3 := '201-32-22 17:25:00'
&Tmp-String-4 := "%(sqldate:%{Tmp-String-3})"

# This shouldn't exist, as the RHS above is NULL, and therefore the assignment will fail
if (&Tmp-String-4) {
	test_fail
}

if (&Module-Failure-Message != "Failed to parse time string \"201-32-22 17:25:00\" as format '\%Y-\%m-\%d \%H:\%M:\%S'") {
	test_fail
}

# Invalid type
&NAS-IP-Address := "192.168.1.1"
&Tmp-String-5 := %(date:%{NAS-IP-Address})

if (&Tmp-String-5) {
	test_fail
}

if (&Module-Failure-Message != "Can't convert type ipaddr into date") {
	test_fail
}

success
